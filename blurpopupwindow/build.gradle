apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def VERSION_NAME = "1.0.9"
def VERSION_CODE = 3
def GROUP = "com.kyleduo.blurpopupwindow"

version = VERSION_NAME
group = GROUP

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode VERSION_CODE
        versionName VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 25
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //noinspection GradleCompatible
    compile 'com.android.support:appcompat-v7:25.3.0'
    testCompile 'junit:junit:4.12'
}

ext {
    POM_ARTIFACT_ID = 'blurpopupwindow'
    POM_NAME = 'BlurPopupWindow'
    POM_PACKAGING = 'aar'
    GIT_URL = 'https://github.com/kyleduo/BlurPopupWindow.git'
    PRJ_URL = 'https://github.com/kyleduo/BlurPopupWindow'
}

install {
    repositories.mavenInstaller {
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                packaging POM_PACKAGING
                groupId GROUP
                artifactId POM_ARTIFACT_ID

                // Add your description here
                name POM_NAME
                url PRJ_URL

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'kyleduo'
                        name 'kyleduo'
                        email 'kyleduo@gmail.com'
                    }
                }
                scm {
                    connection GIT_URL
                    developerConnection GIT_URL
                    url PRJ_URL

                }
            }
        }
    }
}
if (project.getPlugins().hasPlugin('com.android.application') ||
        project.getPlugins().hasPlugin('com.android.library')) {
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = POM_NAME
        userOrg = 'kyleduo'
        licenses = ['Apache-2.0']
        websiteUrl = PRJ_URL
        vcsUrl = GIT_URL
        publicDownloadNumbers = true
        override = true
        publish = true

        version {
            name = VERSION_NAME
            desc = 'A library for Android for popup window with blur background.'
            released = new Date()
            vcsTag = version
        }
    }
}

bintrayUpload.dependsOn install
